/*
** Copyright (C) 2016  International Business Machines Corporation
** All Rights Reserved
*/
namespace com.ibm.streamsx.nlp;

use com.ibm.streamsx.nlp.internal::*;
use com.ibm.streamsx.nlp.utils::*;

/**
 * This operator builds a model on the basis of a training set of data whose category membership is known.
 *
 * The LinearClassificationModelBuilder operator should be used in a Streams release where SPL Python primitive support is not provided (e.g. Streams 3.2). It uses a **ShellSource** operator to invoke Python scripts.
 * When using a Streams release 4.2 or later, it is recommended to create a *SPL Python primitive operator* to invoke Python classes or functions.
 *
 * @param pythonCommand
 * The name of the python binary. The default name is `python`.
 * With this parameter you can change the version and the location of the python command according to your environment.
 * Linear classification scripts need python 2.7 or later!
 *
 * @param pythonScript
 * The name of the python script. The default is `<toolkit_dir>/etc/python/LinearClassificationModelBuilder.py`.
 *
 * @param trainingFile
 * The name of the input file containing the training data.
 * 
 * @param modelFilesDirectory
 * The name of the output directory where the model pkl files are created.
 */
public composite LinearClassificationModelBuilder
(
) {
	param
		expression<rstring> $pythonCommand: "python";
		expression<rstring> $pythonScript: getToolkitPath()+"/etc/python/LinearClassificationModelBuilder.py";
		expression<rstring> $trainingFile;
		expression<rstring> $modelFilesDirectory;

	graph 

		(stream<rstring result> ResultStream) as LCMB = LinearClassificationModelBuilderCore () {
			param 
				trainingFile: $trainingFile;
				modelFilesDirectory: $modelFilesDirectory;
				pythonCommand: $pythonCommand;
				pythonScript: $pythonScript;
		}

}

/**
 * This operator builds a model on the basis of a training set of data whose category membership is known.
 *
 * @output ResultStream
 * Result stream, window marker at end of building the model
 * 
 * @param pythonCommand
 * The name of the python binary. The default name is `python`.
 * With this parameter you can change the version and the location of the python command according to your environment.
 * Linear classification scripts need python 2.7 or later!
 *
 * @param pythonScript
 * The name of the python script. The default is `<toolkit_dir>/etc/python/LinearClassificationModelBuilder.py`.
 *
 * @param trainingFile
 * The name of the input file containing the training data.
 * 
 * @param modelFilesDirectory
 * The name of the output directory where the model pkl files are created.
 */
composite LinearClassificationModelBuilderCore
( output ResultStream
) {
	param
		expression<rstring> $pythonCommand: "python";
		expression<rstring> $pythonScript: getToolkitPath()+"/etc/python/LinearClassificationModelBuilder.py";
		expression<rstring> $trainingFile;
		expression<rstring> $modelFilesDirectory;

	graph 

		// The stdout stream from the grep command is sent on the operator's first output port; the stderr stream is sent on the 
		// second output port.
		(
			stream<rstring ln> OutStream;
			stream<rstring ln> ErrStream;
			stream<int32 exitCode, rstring exitReason> StatStream as ST
		) = ShellSource() { 
			param
				command: $pythonCommand+" "+$pythonScript+" "+$trainingFile+" "+$modelFilesDirectory;
			output
				ST: exitCode = exitCode(), exitReason = exitReason();
		}

		() as WriterO = Custom(OutStream as I){
			logic
			onTuple I : {
				printStringLn(I.ln);
			}
		}

		() as WriterE = Custom(ErrStream as I) {
			logic
			onTuple I: {
				appTrc(Trace.error, I.ln);
			}
		}

		(stream<rstring result> ResultStream) as WriterStat = Custom(StatStream as I) {
			logic onTuple I: {
				if (exitCode != 0) 
					appTrc(Trace.error, "Model Builder exit code: " + (rstring)exitCode + " reason: " + exitReason);
				printStringLn("Model Builder exit code: " + (rstring)exitCode + " reason: " + exitReason);
				submit(Sys.WindowMarker, ResultStream);
			}
		}

}

/**
 * This operator builds a model on the basis of a training set of data whose category membership is known.
 *
 * @output ResultStream
 * Result stream, window marker at end of building the model 
 *
 * @param pythonCommand
 * The name of the python binary. The default name is `python`.
 * With this parameter you can change the version and the location of the python command according to your environment.
 * Linear classification scripts need python 2.7 or later!
 *
 * @param pythonScript
 * The name of the python script. The default is `<toolkit_dir>/etc/python/LinearClassificationModelBuilder.py`.
 *
 * @param trainingFile
 * The name of the input file containing the training data.
 * 
 * @param modelFilesDirectory
 * The name of the output directory where the model pkl files are created.
 */
public composite LinearClassificationModelBuilder2
( output ResultStream
) {
	param
		expression<rstring> $pythonCommand: "python";
		expression<rstring> $pythonScript: getToolkitPath()+"/etc/python/LinearClassificationModelBuilder.py";
		expression<rstring> $trainingFile;
		expression<rstring> $modelFilesDirectory;

	graph 

		(stream<rstring result> ResultStream) as LCMB = LinearClassificationModelBuilderCore () {
			param 
				trainingFile: $trainingFile;
				modelFilesDirectory: $modelFilesDirectory;
				pythonCommand: $pythonCommand;
				pythonScript: $pythonScript;
		}
}