/*
** Copyright (C) 2016  International Business Machines Corporation
** All Rights Reserved
*/
namespace nlp.sample;

use com.ibm.streamsx.nlp::*;

composite UimaTextGeneratedTypes {
	
	graph

		stream<rstring result> UimaTextGeneratedTypesResult = UimaTextGeneratedTypesComp() {
		}
}
/**
 * UimaTextGeneratedTypesComp is used in UimaTextGeneratedTypes and in unit test Main composite.
 */
public composite UimaTextGeneratedTypesComp (output ResultStream) {
	
	graph 

		stream<rstring text> TextStream = FileSource() {
			param
				format : line;
				file : getThisToolkitDir()+"/opt/"+"input.txt";
		}

		/*
		 * This composite is used as toolkit in the unit test Main composite and 
		 * it is required to set the pearFile parameter with getThisToolkitDir(),
		 * because etc directory is part of this toolkit.
		 * If you would run the UimaTextGeneratedTypesSample, then the pear file would be found also in 
		 * application directory of UimaTextGeneratedTypesSample without the getThisToolkitDir() prefix.
		 * 
		 * The type com_ibm_example_KeyPhrase_type is generated by the createTypes.pl script from the pear file.
		 */
		(stream<rstring text, rstring xmi1, list<com_ibm_example_KeyPhrase_type> annotations> TextAnnotated as O) as UIMA_Text = UimaText(TextStream as I){
			param
				pearFile: getThisToolkitDir()+ "/" + "etc/troubleful9.pear";
				casOut: "xmi1"; // serialized CAS
				outputAttributes: "annotations";
		}

		/**
		 * This is a simple helper to print the results to the console.
		 * In the Streaming Analytics service, go to the Log Viewer and Click on the PE's Console Log to view output
		 */
		(stream<rstring result> ResultStream as O) as OutputPrinter= Custom(TextAnnotated as I){
			logic state: {
				mutable boolean resultSent = false;
			}
			onTuple I : {
				if (spl.collection::size(I.annotations) > 0) {
					printStringLn((rstring)I);
					if (!resultSent) {
						submit ({result="ok"}, O);
						resultSent = true;
					}
				}
			}
		}
}
